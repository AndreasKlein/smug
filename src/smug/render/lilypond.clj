(ns smug.render.lilypond
  (:require [smug.render.model :refer :all]
            [clojure.java.shell :refer [sh]]
            [clojure.java.io :as io]))

(defmulti render-element (fn [[element-type]] element-type))

(defmethod render-element :note
  [[_ pitch duration]]
  (str
   (let [n (name pitch)]
     (if (= (clojure.string/lower-case n) n)
       (str n "'")
       n))
   (if (ratio? duration)
     (denominator duration)
     duration)))

(defmethod render-element :bar-line
  [[_ line-type]]
  (condp = line-type
    :end    "\\bar \"|.\""
    :single "\\bar \"|\""))

(defmethod render-element :end-of-staff-line
  [_]
  "\n  ")

(defn render-svg-to [score f]
  (let [elements (score->elements score)
        rendered-elements (clojure.string/join " "
                           (map render-element elements))
        document (str "
\\header {
  title = \"Sight Reading Exercise\"
  tagline = \"Generated by SMUG\"
}
{\n  "
                      rendered-elements
                      "\n}")
        dir (.getParent f)
        name (clojure.string/replace (.getName f) ".svg$" "")
        lilypond-output (io/file dir name)
        output-file (io/file dir (str name ".svg"))]
    (let [{:keys [exit out err]} (sh "lilypond"
                                     "-dbackend=svg"
                                     (str "--output=" lilypond-output)
                                     "-"
                                     :in document)]
      (if (= exit 0)
        output-file
        (throw (ex-info "Failed to render score using Lilypond"
                        {:stdout out
                         :stderr err}))))))

